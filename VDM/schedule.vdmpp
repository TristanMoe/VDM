class schedule 

instance variables
    public schedule_actions: seq of action := [];
    public schedule_route : route; 
    public train_owner_id : train`train_id; 
    -- TODO?: inv schedule_inv(schedule_actions, schedule_route); 

types 
   public action = <ADVANCE> | <WAIT> | <FINAL>; 

operations
    public schedule: train`train_id * route ==> schedule
    schedule(t_id, route) == 
    ( 
        train_owner_id := t_id; 
        schedule_route := route;
    );

    pure public get_owner_id: () ==> train`train_id 
    get_owner_id() == 
        return train_owner_id; 

    public push_action: action ==> ()
    push_action(act) == 
    (
        schedule_actions := schedule_actions ^ [act]; 
    )
    -- Must not be finalized yet. 
    pre not schedule_final_inv()
    post schedule_len_inv(schedule_actions, schedule_route);

    public pop_action: () ==> action 
    pop_action() == 
    (
        dcl next : action := hd schedule_actions; 
        schedule_actions := tl schedule_actions; 
        return next; 
    )
    pre len schedule_actions > 0;

    pure public get_actions: () ==> seq of action
    get_actions() == 
        return schedule_actions
    pre schedule_len_inv(schedule_actions, schedule_route) and schedule_final_inv();

    pure public destination_part_of_schedule: () ==> bool 
    destination_part_of_schedule() == 
        return schedule_final_inv();

    -- Schedule should not advance more than tracks/connections in route map. 
    pure schedule_len_inv: seq of action * route ==> bool 
    schedule_len_inv(act, r_m) == 
        return len [a | a in seq act & a = <ADVANCE>] <= len r_m.get_stations(); 

    -- If final is already in schedule, should not be allowed to add actions. 
    pure schedule_final_inv: () ==> bool 
    schedule_final_inv() == 
        return if schedule_actions <> [] then hd reverse schedule_actions = <FINAL> else false;

    public print: () ==> () 
    print() == 
    (
        IO`print("#"); 
        IO`print(train_owner_id);
        IO`print(" Schedule: [ ");
        for act in schedule_actions do 
        (
            IO`print(act);  
            IO`print(" ");
        );
        IO`println("]");
    ); 

    -- TODO: Adaptive scheduling based on missing signal for given cycle? 
    --      It should 'delay' schedule until signal is recieved. 
    --      Might be train class that should be responsible. 
end schedule 